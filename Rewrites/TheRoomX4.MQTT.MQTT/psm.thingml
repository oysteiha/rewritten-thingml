/* This is the PSM the Platform Specific Model */
/*** This version contains the simple simulated based on the MQTT PSM platform */

/* This is the first very hard-coded Z-wave version 190105 */
/* This version is very hardcoded for the openhabianpi version 190206 */

/* This version has possibility to change the polling intervals for temperature */
import "datatypes.thingml" from stl	
import "pim_messages.thingml"


protocol MQTT @serializer "JSON"
  @mqtt_broker_address "localhost"
  @mqtt_broker_port "1883" 
  @mqtt_publish_topic "CPS2021/SwitchControl"
  @mqtt_subscribe_topic "CPS2021/tempoutput";


thing MQTTDriver includes TemperatureMsg, OnOffMsg, GeneralMsg {
	required port to_pim {sends temperature, heatOn, heatOff, set_temperature, set_delta, fetch_temp, set_night_start, set_day_start, set_luminance}
	provided port from_pim {receives add_thermometer, add_device, SwitchOn, SwitchOff, prompt}
	
	required port to_frz {sends temperature}
	provided port from_frz {receives temperature, prompt, SwitchOn, SwitchOff}

	//Only one MQTT port to receive the sensor values
	provided port MQTT {
		receives luminance, temperature, heatOn, heatOff, set_temperature, set_delta, fetch_temp, set_day_start, set_night_start, set_luminance
		sends SwitchOn,SwitchOff
	}

	property thermometers:Integer[25] // Identifiers of the thermometers in the set
	property thermotext:String[25] // corresponding explanatory text 
	property thermoval:Double[25] // values to report from the corresponding thermometer
	
	property lightsensors:Integer[25] // Identifiers of the lightsensors in the set
	property lightval:Double[25] // values to report from the corresponding lightsensor
	
	property motionsensors:Integer[25] // Identifiers of the thermometers in the set
	
	property last_mo:Integer = 0 // number of motion sensors in the set  
	property last_light:Integer = 0 // number of light sensors in the set  
	property last_thermo:Integer = 0 // number of thermometers in the set 
	property foundL:Boolean // temporary - true when item found in loop
	property i:Integer // runner index in list
	property id_s:Integer // temporary id value (to be used with kick-down)
	property found:Boolean // temporary - true when item found in loop
	
	
	property onoffswitches:Integer[25] // Identifiers of the onoffswitches in the set
	property last_onoff:Integer = 0 // number of onoff switches in the set 		
	property did:Integer // temporary device id
	
	statechart behaviorMQTTDriver init Idle {
		state Idle{
			transition -> Idle
			event put_t:MQTT?temperature
			action do
				id_s=put_t.id
				i=0
				found = false
				while (i<last_thermo and (not found)) do
					if (id_s==thermometers[i]) do
						found=true // trick to terminate while loop
					end
					i=i+1
				end
				if (found) do
					thermoval[i-1]=put_t.t
					to_pim!temperature(id_s,thermotext[i],put_t.t)				
				end
			end
						
			transition -> Idle
			event addt:from_pim?add_thermometer
			action do				
				id_s=addt.id
				thermometers[last_thermo]=id_s
				thermotext[last_thermo]=addt.txt
				thermoval[last_thermo]= 20 // just a default temperature
				last_thermo = last_thermo+1
			end

			transition -> Idle
			event add_dev: from_pim?add_device
			action do
				did=add_dev.did
				
				onoffswitches[last_onoff]=did
				last_onoff = last_onoff+1
			end
			transition -> Idle
			event swon: from_pim?SwitchOn
			action do
				did=swon.did
				i=0
				found = false
				while (i<last_onoff and (not found)) do
					if (did==onoffswitches[i]) do
						found=true // trick to terminate while loop
					end
					i=i+1
				end
				if (found) do
					MQTT!SwitchOn(did)
				end
			end
			transition -> Idle
			event swoff: from_pim?SwitchOff
			action do
				did=swoff.did
				i=0
				found = false
				while (i<last_onoff and (not found)) do
					if (did==onoffswitches[i]) do
						found=true // trick to terminate while loop
					end
					i=i+1
				end
				if (found) do
					MQTT!SwitchOff(did)
				end
			end			
			transition -> Idle
			event hon: MQTT?heatOn
			action do
				did=hon.did
				i=0
				found = false
				while (i<last_onoff and (not found)) do
					if (did==onoffswitches[i]) do
						found=true // trick to terminate while loop
					end
					i=i+1
				end
				if (found) do
					to_pim!heatOn(did)
				end
			end
			transition -> Idle
			event hoff: MQTT?heatOff
			action do
				did=hoff.did
				i=0
				found = false
				while (i<last_onoff and (not found)) do
					if (did==onoffswitches[i]) do
						found=true // trick to terminate while loop
					end
					i=i+1
				end
				if (found) do
					to_pim!heatOff(did)
				end
			end	
			transition -> Idle
			event pr: from_pim?prompt
			action do
				println(pr.txt)
			end		
			transition -> Idle
			event st: MQTT?set_temperature
			action do
				to_pim!set_temperature(st.t)
			end		
			transition -> Idle
			event givdlt: MQTT?set_delta
			action do
				to_pim!set_delta(givdlt.dlta)
			end		
			transition -> Idle
			event ft: MQTT?fetch_temp
			action do
				to_pim!fetch_temp()
			end		
			
			transition -> Idle
			event addlum:from_pim?add_lightsensor
			action do				
				id_s=addlum.id
				lightsensors[last_light]= id_s // default luminance level
				lightval[last_light] = 50
				last_light = last_light+1
			end

			transition -> Idle
			event put_lum:MQTT?luminance
			action do
				id_s=put_lum.id
				
				i=0
				foundL = false
				while (i<last_light and (not foundL)) do
					if(id_s==lightsensors[i]) do
						foundL = true //Terminates loop
					end
					i=i+1
				end
				if (foundL) do
					lightval[i-1]=put_lum.lum
					to_pim!luminance(id_s, put_lum.lum)
				end
			end
			
			transition -> Idle
			event evn: MQTT?set_day_start
			action do
				to_pim!set_day_start(evn.time)
			end	
			
			transition -> Idle
			event evnt: MQTT?set_night_start
			action do
				to_pim!set_night_start(evnt.time)
			end	
			
			transition -> Idle
			event evnt: MQTT?set_luminance
			action do
				to_pim!set_luminance(evnt.lum)
			end				
		}
	}
 }// MQTTDriver

